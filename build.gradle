plugins {
    id "com.jfrog.bintray" version "1.2"
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group = 'de.wellnerbou'
version = '2.0'

description = """git-changelog"""

mainClassName = "de.wellnerbou.gitchangelog.app.GitChangelog"

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

// user and key configuration for bintray deployment:
// ./gradlew bintrayUpload -PpublishUser=<USER> -PpublishKey=<KEY>
if (!project.hasProperty("publishUser")) {
    ext.publishUser = ""
}
if (!project.hasProperty("publishKey")) {
    ext.publishKey = ""
}

run {
    if ( project.hasProperty('args') ) {
        args project.args.split('\\s+')
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'git-changelog'
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

dependencies {
    compile ('org.eclipse.jgit:org.eclipse.jgit:4.0.3.201509231615-r')
    compile 'com.google.guava:guava:18.0'
    compile 'net.sf.jopt-simple:jopt-simple:4.8'
    testCompile "junit:junit:4.11"
    testCompile 'org.assertj:assertj-core:2.1.0'
//    testCompile 'org.assertj:assertj-core:3.0.0' for java 8
}

bintray {
    user = "$publishUser"
    key = "$publishKey"
    publications = ['mavenJava']

    dryRun = false
    publish = true

    pkg {
        repo = 'maven'
        name = 'git-changelog'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/paulwellnerbou/git-jira-log'
    }
}
